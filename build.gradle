buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'maven-publish'

version = '1.0.3'
group = 'jp.ngri'
archivesBaseName = 'SchoolProject'

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
    runDir = 'eclipse'
}

dependencies {
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task replaceVersion(type: Copy) {
    inputs.property 'version', project.version
    from projectDir.path
    include 'SchoolProjectVersion.java'
    into sourceSets.main.java.srcDirs[0].path + '/jp/ngri/schoolproject'
    duplicatesStrategy 'include'
    expand 'version': project.version
    System.out.println(projectDir.path)
    System.out.println(sourceSets.main.java.srcDirs[0].path + '/jp/ngri/schoolproject')
}

compileJava.dependsOn.add(replaceVersion)

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        schoolProject(MavenPublication) {
            artifact('build/libs/' + archivesBaseName + '-' + version + '.jar') {
                classifier = ''
                extension = 'jar'
            }
            artifact javadocJar {
                classifier = 'javadoc'
                extension = 'jar'
            }
            artifact sourcesJar {
                classifier = 'sources'
                extension = 'jar'
            }
        }
    }
    repositories {
        maven {
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username publishUser
                password publishPass
            }
            url 'https://www.ngri.jp/repos/maven/'
        }
    }
}

publish.dependsOn.add(build)
